title: Opening
tags:
position: 12, -102
---
//<<wait 1>>
<<play_sound Clock "environment">>
<<set_object_position Player -2.22 -22.2>>
<<enable_textboxes false>>
<<enable_textboxes true>>
SOMNI: ...Where am I?
<<enable_textboxes false>>
<<move_player 2 0 1>>
<<wait 1>>
<<change_player_direction 3>>
<<enable_textboxes true>>
SOMNI: I don't know why, but I wanna go outside...
<<set_progression_flag "Watched Opening" true>>
<<show_movement_text>>
===

title: First_Outside
tags:
position: 12, -102
---
//fade to black
<<enable_textboxes false>>
<<fade_to_black>>
<<wait 1>>
//move SOMNI to new room
<<set_object_position Player 168.309 -45.5>>
//wait
<<wait 0.5>>
<<fade_from_black>>
<<wait 2>>
//change sprite of door
<<set_alpha house_door 0>>
//play sound of door opening
<<play_sound DoorOpen>>
//wait
<<wait 2>>
<<enable_textboxes true>>
SOMNI: Oh.
SOMNI: There's nothing.
<<enable_textboxes false>>
<<wait 0.5>>
//change sprite of door
<<set_alpha house_door 1>>
//play sound of door closing
<<play_sound DoorClose>>
<<wait 1>>
//fade to black
<<fade_to_black>>
//go back to previous room
<<wait 1>>
<<set_object_position Player 90.52 -44.2>>
//fade from black
<<wait 0.5>>
<<fade_from_black>>
<<play_sound Clock "environment">>
<<wait 1>>
<<enable_textboxes true>>
SOMNI: Welp. That's terrifying.
SOMNI: I guess I'll go back to bed.
<<set_progression_flag "Saw Void" true>>
===

title: Back_To_Bed
tags:
position: 12, -102
---
//walk command
<<enable_textboxes false>>
<<move_player -1.13 -22.08 0.2 false>>
<<wait 0.2>>
<<move_player -2.238 -22.08 0.5 false>>
<<wait 0.5>>
<<change_player_direction 3>>
<<enable_textboxes true>>
SOMNI: I'll just stay here...
<<enable_textboxes false>>
//fade to black
<<fade_to_black>>
//wait
<<wait 4>>
<<set_light_intensity "Global Lighting" 0.5>>
//<<play_sound Explosion1>>
<<play_sound Explosion1>>
//end black
<<cut_from_black>>
//wait
<<wait 1>>
<<enable_textboxes true>>
SOMNI: What was that...?
<<enable_textboxes false>>
//walk
<<move_player 2 0 1>>
<<wait 1>>
<<change_player_direction 3>>
//wait
<<wait 0.5>>
//fade to black
<<fade_to_black>>
//wait
<<wait 2>>
<<set_object_position Player -0.19 -22.29>>
//fade from black
<<fade_from_black>>
<<instantiate "Guide Crash" 96.02 -38.73>>
<<instantiate "Debris" 96.01 -38.95>>
<<set_light_intensity "Crash Guide Spotlight" 1>>
<<set_progression_flag "Second Awake" true>>
===

title: Meet_Guide
tags:
position: 12, -102
---
<<enable_textboxes false>>
<<wait 0.1>>
<<move_player 96.02 -39.7 0.5 false>>
<<wait 0.5>>
<<change_player_direction 1>>
<<fade_out_source 3 "environment">>
<<enable_textboxes true>>
SOMNI: Um... hey there. Are you okay?
<<enable_textboxes false>>
//wait
<<wait 1>>
<<enable_textboxes true>>
SOMNI: Hey. So uh...I can't help but notice that there's this hole in the ceiling that is...right above you. And-
<<enable_textboxes false>>
//GUIDE gets up
<<set_anim_trigger Guide "Stand Up">>
//wait
<<wait 1.7>>
<<set_anim_bool Guide "Idle" true>>
<<enable_textboxes true>>
GUIDE: YOU!
<<enable_textboxes false>>
//camera follows GUIDE
//GUIDE begins levitating and floats into the air
//start playing music
<<set_anim_trigger Guide "Ascend">>
<<wait 0.45>>
<<music_layers "spdemo1_medium,spdemo1_novocals,spdemo1_full">>
<<move_object Guide 0 5 1.48>>
<<set_camera_target Guide>>
<<wait 4>>
<<enable_textboxes true>>
SOMNI: ...Me?
SOMNI: What did <i>I</i> do?
GUIDE: I'm...gonna...KILL YOU! //"KILL YOU" is in shaking red text
<<enable_textboxes false>>
//explosion and transition to boss room
<<play_sound "Explosion2">>
<<fade_to_white>>
<<wait 0.35>>
<<set_object_position Player 96.02 -79.81>>
<<set_object_position Guide 96.02 -73.83>>
<<set_object_position MainCamera 96.02 -73.83>>
<<set_anim_trigger Guide "Horns">>
<<fade_from_white>>
<<wait 3>>
<<set_camera_target Player>>
<<instantiate "Guide Boss" 96.02 -73.83>>
<<set_object_position Guide 1000 1000>>
<<reset_all_anim_triggers Guide>>
<<set_anim_bool Guide "Idle" false>>
<<change_music_layer>>
<<set_light_intensity "Fireplace Glow" 0>>
//wait
//<<set_progression_flag "Guide Interact" true>>
===

title: After_Guide
tags:
position: 12, -102
---
<<declare $choice = "" as string>>
//fade to white
<<set_light_intensity "Crash Guide Spotlight" 0>>
<<destroy_object Debris>>
<<destroy_object GuideBoss>>
<<clear_bullets>>
<<enable_textboxes false>>
//explosion and transition to boss room
<<play_sound "Explosion2">>
<<fade_to_white>>
<<wait 0.35>>
//put characters in correct location
<<change_player_direction 1>>
<<set_object_position Player 96 -77.47>>
<<set_object_position Guide 96.02 -75.163>>
<<set_anim_trigger Player "Hold Attack">>
//fade from white
<<fade_from_white>>
<<wait 0.25>>
<<enable_textboxes true>>
SOMNI: Hey...are you good now?
<<enable_textboxes false>>
<<wait 0.1>>
<<set_anim_trigger Guide "Stand Up">>
<<wait 2>>
<<set_anim_bool Guide "Idle" true>>
<<enable_textboxes true>>
GUIDE: Uhhhngh...what happened...
GUIDE: AH! Wait, wait! I'm good! I'm good!
SOMNI: ...
//GUIDE turns around
<<set_anim_trigger Player "Back To Loop">>
<<change_player_direction 1>>
<<set_anim_int Guide "direction" 1>>
GUIDE: Phew...It's okay. It's okay! Inhale, *breathes in*, exhale, *breathes out*.
SOMNI: ...
//GUIDE turns around
<<set_anim_int Guide "direction" 0>>
//start playing music
<<music_layers "unforeseen">>
GUIDE: I'm so sorry, sir. This was incredibly unprofessional of me and I hope this won't affect an post-procedure reviews.
SOMNI: Um...sure. Could you just-
GUIDE: Do you remember your name?
SOMNI: Uh...yeah. It's SOMNI.
GUIDE: Perfect! Had me worried there for a second.
SOMNI: Well. I certainly remember my name, I guess.
GUIDE: Wait, do you not know about any of this?
SOMNI: What "this" are you referring to exactly?
SOMNI: Who are you? What's going on?
GUIDE: Wha- //Autoadvance
GUIDE: Oh...right.
GUIDE: You don't remember <i>anything.</i> I don't know why we erase the briefings, but I guess-
SOMNI: Can you just start from the beginning?
GUIDE: Well I'm glad you asked! 
<<enable_textboxes false>>
//takes out pamplet
<<set_anim_trigger Guide "Pamphlet">>
<<wait 1>>
<<play_sound "Paper">>
<<wait 0.5>>
<<enable_textboxes true>>
GUIDE: You can call me <b>GUIDE.</b>
GUIDE: I'm here to help you perform this procedure.
GUIDE: We are currently talking in your mind. In the waking world, your body is currently in stasis in order for us to operate.
SOMNI: Operate?
GUIDE: You, SOMNI, have come to <b>Paths Inc.</b> in order to undergo a procedure that will effectively "update" your emotions by putting you through scenarios specifically designed to trigger the emotions you requested to be updated.
GUIDE: ...Which in this case is <color=blue>Sadness</color>, <color=red>Anger</color>, and <color=yellow>Happiness</color>.
GUIDE: ...Wow. These three? How original.
<<enable_textboxes false>>
//puts away pamplet
<<set_anim_trigger Guide "Pamphlet Close">>
<<wait 0.2>>
<<play_sound "Paper">>
<<wait 1.3>>
<<enable_textboxes true>>
SOMNI: Do you treat all your clients like this?
GUIDE: Some customers have complained about my occasionally erudite demeanor.
SOMNI: I meant crashing through their ceiling and jumping them with demon powers.
GUIDE: Once again, apologies for the unprofessionalism.
GUIDE: I was simply surprised by the...uh...state of your <b>mindscape</b>.
GUIDE: Clients don't typically have a single structure floating through an endless void.
SOMNI: For a GUIDE, you're not very good at explaining.
GUIDE: Well, you'll just need to figure it out as we go then. We have to get going.
SOMNI: No. I’m good.
GUIDE: Um… what?
SOMNI: I’m fine. Send me back to the real world. This seems sketchy as hell and I’m not taking any risks.
GUIDE: Sir, if I could change your mind-
SOMNI: Why? So you can get paid. Clearly I made a mistake because I put my brain in the hands of a corporation.
GUIDE: If you just let me explain- //Auto Advance
SOMNI: I don’t care how much you explain! 
SOMNI: I’m not just gonna trust someone who breaks into my mind and says they're performing a procedure that I don’t even remember signing up for. Send me back.
GUIDE: ...
SOMNI: ...
GUIDE: Listen…
SOMNI: ...
GUIDE: If you want to go back, I can send you back. No one is making you do this. But know that you came to us for a reason. 
GUIDE: I have no information on what your situation is out there, but from what I’ve seen, I know that you wouldn’t be here if you had any other choice.
SOMNI: ...
<<jump Are_You_Ready>>
===

title: Are_You_Ready
tags:
position: 12, -102
---
<<declare $choice2 = "" as string>>
GUIDE: Are you ready to begin?
-> SOMNI: I guess....
    <<set $choice2 to "A">>
-> SOMNI: Not yet.
    <<set $choice2 to "B">>
<<if $choice2 == "A">>
    GUIDE: That's the spirit.
    //GUIDE snaps her fingers and the room is filled with white light.
    //snap sound
    <<enable_textboxes false>>
    <<fade_to_white>>
    <<set_light_intensity "Global Lighting" 1>>
    <<wait 0.35>>
    <<set_object_position Player 94.63 -38.77>>
    <<set_object_position Guide 97.49 -38.77>>
    <<wait 0.3>>
    <<fade_from_white>>
    <<wait 1>>
    <<change_player_direction 2>>
    <<wait 0.25>>
    <<change_player_direction 3>>
    <<wait 0.25>>
    <<change_player_direction 0>>
    <<wait 0.2>>
    <<enable_textboxes true>>
    SOMNI: What happened?
    <<enable_textboxes false>>
    //Pan camera to front door
    <<pan_camera 90.53 -45.79 2>>
    <<wait 2>>
    <<enable_textboxes true>>
    GUIDE: Go through the front door.
    SOMNI: You mean the one with nothing on the other side?
    GUIDE: Precisely.
    <<enable_textboxes false>>
    //Pan camera back
    <<set_camera_target Player>>
    <<wait 0.5>>
    <<enable_textboxes true>>
    SOMNI: Okay but if I die I'm suing.
    <<set_progression_flag "Leave House First Time" true>>
<<elseif $choice2 == "B">>
    SOMNI: Just need a little more time to get my bearings.
    GUIDE: All right. Just let me know when you're ready!
    <<set_progression_flag "Not Ready" true>>
<<endif>>
===

title: Guide_Interact_After_Boss
tags:
position: 12, -102
---
GUIDE: What are you standing around for?
GUIDE: Let's get going!
SOMNI: Yeah, yeah.
===

title: Second_Outside
tags:
position: 12, -102
---
//fade to black
<<set_anim_bool Guide "Idle" true>>
<<fade_music_layer 1>>
<<enable_textboxes false>>
<<fade_to_black>>
<<wait 1>>
//move SOMNI to new room
<<set_object_position Player 152.52 -95.15>>
<<set_object_position Guide 152.52 -99>>
//wait
<<wait 0.5>>
<<fade_from_black>>
<<wait 1>>
//change sprite of door
<<set_alpha house_door_2 0>>
//play sound of door opening
<<play_sound DoorOpen>>
//wait
<<wait 2>>
<<move_player 0 -2 1>>
<<wait 1>>
<<set_alpha house_door_2 1>>
//play sound of door opening
<<play_sound DoorClose>>
<<wait 0.2>>
<<enable_textboxes true>>
SOMNI: How did you...but you were-
GUIDE: Let's just call it some GUIDE magic. //magic is in wavy text
GUIDE: Now get over here. There's something I want to show you.
<<enable_textboxes false>>
//sound for fading
<<fade_to_white>>
<<wait 0.35>>
<<set_anim_int Guide "direction" 1>>
<<set_object_position Guide 160.47 -99.206>>
<<fade_from_white>>
<<object_layer house_door_2 10>>
<<wait 0.3>>
<<set_progression_flag "Second Outside" true>>
===

title: Try_Leave
tags:
position: 12, -102
---
<<set_anim_int Guide "direction" 0>>
GUIDE: Hey! Where are you going?
SOMNI: ...I can go outside if I want. It is MY mind after all.
GUIDE: Uhhhh...no can do. Get back over here.
<<enable_textboxes false>>
<<move_player 0 1 0.5>>
<<wait 0.5>>
<<set_anim_int Guide "direction" 1>>
===

title: Gravestone_Interact
tags:
position: 12, -102
---
//move player to specific location
<<enable_textboxes false>>
<<wait 0.1>>
<<move_player 161.525 -99.206 0.5 false>>
<<wait 0.5>>
<<change_player_direction 1>>
<<enable_textboxes true>>
SOMNI: What's that?
GUIDE: Wait for it.
<<enable_textboxes false>>
<<wait 0.5>>
//GHOST DOG rises from the grave
<<move_object GhostDog 0 1 1>>
<<fade_object GhostDog 1 1>>
<<wait 1.2>>
<<enable_textboxes true>>
SOMNI: WHOAAAAA! A DOG IN A BEDSHEET! I take it back this is dope!
<<enable_textboxes false>>
//SOMNI runs up to GHOST DOG but runs straight through him
<<move_player 0 2 0.5>>
<<fade_object GhostDog 0 0.25>>
<<wait 0.25>>
<<object_layer GhostDog 13>>
<<fade_object GhostDog 1 0.25>>
<<wait 0.4>>
<<change_player_direction 3>>
<<enable_textboxes true>>
SOMNI: HE'S A <i>GHOST</i> TOO?
GHOST DOG: hi.
SOMNI: ...
SOMNI: HE TALKS?!
GUIDE: Yeah, he talks.
GHOST DOG: tweat.
SOMNI: What's he saying?
GHOST DOG: tweat.
GUIDE: I think it's pretty self-explanatory.
GHOST DOG: tweat.
SOMNI: Uhhhh...do you have any treats?
GUIDE: Ah, sorry. No.
SOMNI: What!?
SOMNI: You're telling me you created a dog that <i>only</i> wants treats, and you have <i>no treats?</i>
GUIDE: Now you're getting it.
SOMNI: You're a monster.
GUIDE: ...
GUIDE: Yeah...welp. I have other, uh, GUIDE business to attend to.
GUIDE: Good luck figuring this out!
<<enable_textboxes false>>
//GUIDE fades out
<<fade_to_white>>
<<wait 0.35>>
<<set_object_position Guide 10000 1000>>
<<fade_from_white>>
<<wait 0.3>>
<<enable_textboxes true>>
SOMNI: You know what? We don't need you! We'll find a treat all by ourselves!
SOMNI: Right, GHOST DOG?
GHOST DOG: bark.
SOMNI: All right. Cool.
GHOST DOG: tweat.
SOMNI: Okay, let's see if I have anything for you in the kitchen.
GHOST DOG: yay.
<<enable_textboxes false>>
<<move_object GhostDog 0 1 1>>
<<fade_object GhostDog 0 1>>
<<wait 1.2>>
<<enable_textboxes true>>
SOMNI: What's up with everyone disappearing on me...
<<enable_textboxes false>>
<<set_progression_flag "Met Ghost Dog" true>>
===

title: Try_Leave_GD
tags:
position: 12, -102
---
GHOST DOG: tweat.
SOMNI: Yeah...you're right.
<<move_player 0 1 0.5>>
<<wait 0.5>>
===

title: Enter_House
tags:
position: 12, -102
---
<<enable_textboxes false>>
<<fade_to_black>>
<<play_sound DoorOpen>>
<<wait 1>>
//move SOMNI to new room
<<set_object_position Player -55.5 -267.78>>
<<set_anim_int Guide "direction" 1>>
<<set_object_position Guide -55.5 -264.89>>
//wait
<<wait 0.5>>
<<fade_from_black>>
<<play_sound DoorClose>>
<<wait 1>>
<<enable_textboxes true>>
GUIDE: ...!
<<enable_textboxes false>>
<<pan_camera -55.5 -264.89 1>>
<<wait 2>>
<<set_anim_int Guide "direction" 0>>
<<enable_textboxes true>>
GUIDE: SOMNI! Hi! How ya doin'?
SOMNI: What did you do? What happened to my house?
GUIDE: Funny story...
GUIDE: I have no idea. But I'm working on it!
<<pan_camera -56.468 -262.46 1>>
SOMNI: Why is there a toilet here?
GUIDE: Oh yeah...
GUIDE: Soooo...because everything seems to be randomly moving itself around, I'm gonna need you to <color=green>SAVE</color> your progress periodically in case I need to reset.
SOMNI: Reset?
GUIDE: Don't worry about it! I've got it under control.
GUIDE: Go find your dog a treat! I'll be back.
<<enable_textboxes false>>
//GUIDE fades out again
<<fade_to_white>>
<<wait 0.35>>
<<set_object_position Guide 10000 1000>>
<<fade_from_white>>
<<wait 0.5>>
<<set_camera_target Player>>
<<wait 0.5>>
<<enable_textboxes true>>
SOMNI: I hate it when she does that. I know I've only seen her do it twice, but I hate it.
SOMNI: What is she even doing? How hard could it possibly be to build an entire world in another person's mind?
GHOST DOG: tweat.
SOMNI: Hey, GHOST DOG. What sound does a bird make?
GHOST DOG: tweat.
SOMNI: Great. Thanks.
<<set_progression_flag "Started Level" true>>
===

title: GhostDog_Interact
tags:
position: 12, -102
---
GHOST DOG: tweat.
SOMNI: Don't worry buddy we'll get you one soon.
GHOST DOG: yay.
===
title: Goodbye_GhostDog
tags:
position: 12, -102
---
//Edit this node later to work with the current version of the script
GHOST DOG: nom nom. tenks.
SOMNI: Anything for you, GHOST DOG.
GHOST DOG: nom nom nom.
SOMNI: ...
GHOST DOG: yum. all done.
SOMNI: Great! Now what?
GHOST DOG: bye.
SOMNI: What? "Bye?"
GHOST DOG: SOMNI make GHOST DOG happy. business finished.
SOMNI: Wait, what? What do you- //auto advance
GHOST DOG: miss u.
//GHOST DOG fades away into nothingness
GUIDE: Okay, I think I got everything stabilized. Should be smooth sailing from here on.
SOMNI: ...
GUIDE: SOMNI? You okay?
SOMNI: He's...he's gone.
GUIDE: Oh, crap. I forgot to tell you...
GUIDE: Listen, I know this process isn't easy, but I think it's important to remember that- // auto advance
//A hole appears below SOMNI, sucking him in.
SOMNI: AHH! GUIDE!
GUIDE: SOMNI!
===

title: Obtained_Treat
tags:
position: 12, -102
---
//variable set up
<<declare $treatCount = 0 as number>>
<<set $treatCount = $treatCount + 1>>
SOMNI: Treat obtained!
SOMNI: Hopefully that makes GHOST DOG happy.
===

title: Check_Treat
tags:
position: 12, -102
---
<<if $treatCount < 3>>
    GHOST DOG: We need more treats!
    GHOST DOG: About {3 - $treatCount} more should do.
    <<enable_textboxes false>>
    <<move_player 0 -1 0.5>>
    <<wait 0.5>>
<<else>>
    <<destroy_object "Treat Trigger">>
    GHOST DOG: We have enough treats!
    <<enable_textboxes false>>
    <<fade_object "Ghost Dog - Treat Check" 0 0.5>>
    <<wait 0.6>>
    <<destroy_object "Ghost Dog - Treat Check">>
<<endif>>
===