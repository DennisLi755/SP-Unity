title: Opening
tags:
position: 12, -102
---
//<<wait 1>>
<<enable_textboxes false>>
<<player_collision false>>
<<enable_textboxes true>>
Somni: ...Where am I?
<<enable_textboxes false>>
<<move_player 2 0 1>>
<<wait 1>>
<<change_player_direction 1>>
<<enable_textboxes true>>
Somni: I don't know why, but I wanna go outside...
<<player_collision true>>
<<set_progression_flag "Watched Opening" true>>
===

title: First_Outside
tags:
position: 12, -102
---
//fade to black
<<enable_textboxes false>>
<<player_collision false>>
<<fade_to_black>>
<<wait 1>>
//move Somni to new room
<<set_object_position Player 168.309 -45.5>>
//wait
<<wait 0.5>>
<<fade_from_black>>
<<wait 2>>
//change sprite of door
<<set_alpha house_door 0>>
//play sound of door opening
<<play_sound DoorOpen>>
//wait
<<wait 2>>
<<enable_textboxes true>>
Somni: Oh.
Somni: There's nothing.
<<enable_textboxes false>>
<<wait 0.5>>
//change sprite of door
<<set_alpha house_door 1>>
//play sound of door closing
<<play_sound DoorClose>>
<<wait 1>>
//fade to black
<<fade_to_black>>
//go back to previous room
<<wait 1>>
<<set_object_position Player 90.52 -44.2>>
//fade from black
<<wait 0.5>>
<<fade_from_black>>
<<wait 1>>
<<enable_textboxes true>>
Somni: Welp. That's terrifying.
Somni: I guess I'll go back to bed.
<<set_progression_flag "Saw Void" true>>
<<player_collision true>>
===

title: Back_To_Bed
tags:
position: 12, -102
---
//walk command
<<enable_textboxes false>>
<<player_collision false>>
<<move_player -1.13 -22.08 0.2 false>>
<<wait 0.2>>
<<move_player -2.238 -22.08 0.5 false>>
<<wait 0.5>>
<<change_player_direction 1>>
<<enable_textboxes true>>
Somni: I'll just stay here...
<<enable_textboxes false>>
//fade to black
<<fade_to_black>>
//wait
<<wait 4>>
//<<play_sound Explosion1>>
<<play_sound Explosion1>>
//end black
<<cut_from_black>>
//wait
<<wait 1>>
<<enable_textboxes true>>
Somni: What was that...?
<<enable_textboxes false>>
//walk
<<move_player 2 0 1>>
<<wait 1>>
<<change_player_direction 1>>
//wait
<<wait 0.5>>
//fade to black
<<fade_to_black>>
//wait
<<wait 2>>
//fade from black
<<fade_from_black>>
<<player_collision true>>
<<instantiate "Guide Crash" 96.02 -38.73>>
<<set_progression_flag "Second Awake" true>>
===

title: Meet_Guide
tags:
position: 12, -102
---
<<enable_textboxes false>>
<<player_collision false>>
<<wait 0.1>>
<<move_player 96.02 -39.7 0.5 false>>
<<wait 0.5>>
<<change_player_direction 0>>
<<enable_textboxes true>>
Somni: Um... hey there. Are you okay?
<<enable_textboxes false>>
//wait
<<wait 1>>
<<enable_textboxes true>>
Somni: Hey. So uh...I can't help but notice that there's this hole in the ceiling that is...right above you. And-
<<enable_textboxes false>>
//Guide gets up
<<set_anim_trigger Guide "Stand Up">>
//wait
<<wait 1.7>>
<<enable_textboxes true>>
Guide: YOU!
<<enable_textboxes false>>
//camera follows Guide
//Guide begins levitating and floats into the air
//start playing music
<<set_anim_trigger Guide "Ascend">>
<<wait 0.45>>
<<music_layers "spdemo1_medium,spdemo1_novocals,spdemo1_full">>
<<move_object Guide 0 5 1.48>>
<<set_camera_target Guide>>
<<wait 4>>
<<enable_textboxes true>>
Somni: ...Me?
Somni: What did <i>I</i> do?
Guide: I'm... gonna... KILL YOU! //"KILL YOU" is in shaking red text
<<enable_textboxes false>>
//explosion and transition to boss room
<<play_sound "Explosion2">>
<<fade_to_white>>
<<wait 0.35>>
<<set_object_position Player 96.02 -80.31>>
<<set_object_position Guide 96.02 -73.83>>
<<set_object_position MainCamera 96.02 -73.83>>
<<fade_from_white>>
<<wait 3>>
<<set_camera_target Player>>
<<destroy_object Guide>>
<<instantiate "Guide Boss" 96.02 -73.83>>
<<change_music_layer>>
//wait
//<<set_progression_flag "Guide Interact" true>>
===

title: After_Guide
tags:
position: 12, -102
---
<<declare $choice = "" as string>>
Guide: Wait, wait! I'm good! I'm good!
Somni: ...
Guide: Phew...It's okay. It's okay! Inhale, *breathes in*, exhale, *breathes out*.
Somni: ...
Guide: I'm so sorry, sir. This was incredibly unprofessional of me and I hope this won't affect an post-procedure reviews.
Somni: Um...sure. Could you just-
Guide: Do you remember your name?
Somni: Uh...yeah. It's Somni.
Guide: Perfect! Had me worried there for a second.
Somni: Well. I certainly remember my name, I guess.
Guide: Wait, do you not know about any of this?
Somni: What "this" are you referring to exactly?
Somni: Who are you? What's going on?
Guide: Wha-
Guide: Oh...right.
Guide: You don't remember <i>anything.</i> I don't know why we erase the briefings, but I guess-
Somni: Can you just start from the beginning?
Guide: Well I'm glad you asked! You can call me <b>Guide.</b>
Guide: I'm here to help you perform this procedure.
Guide: We are currently talking in your mind. In the waking world, your body is currently in stasis in order for us to operate.
Somni: Operate?
Guide: You, Somni, have come to <b>Paths Inc.</b> in order to undergo a procedure that will effectively "update" your emotions by putting you through scenarios specifically designed to trigger the emotions you requested to be updated.
Guide: ...Which in this case is <color=blue>Sadness</color>, <color=red>Anger</color>, and <color=yellow>Happiness</color>.
Guide: ...Wow. These three? How original.
Somni: Do you treat all your clients like this?
Guide: Some customers have complained about my occasionally erudite demeanor.
Somni: I meant crashing through their ceiling and jumping them with demon powers.
Guide: Once again, apologies for the unprofessionalism.
Guide: I was simply surprised by the...uh...state of your <b>mindscape</b>.
Guide: Clients don't typically have a single structure floating through an endless void.
Somni: For a Guide, you're not very good at explaining.
Guide: Well, you'll just need to figure it out as we go then. We have to get going.
Somni: No. I’m good.
Guide: Um… what?
Somni: I’m fine. Send me back to the real world. This seems sketchy as hell and I’m not taking any risks.
Guide: Sir, if I could change your mind-
Somni: Why? So you can get paid. Clearly I made a mistake because I put my brain in the hands of a corporation.
Guide: If you just let me explain-
Somni: I don’t care how much you explain! I’m not just gonna trust someone who breaks into my mind and says they're performing a procedure that I don’t even remember signing up for. Send me back.
Guide: ...
Somni: ...
Guide: Listen…
Somni: ...
Guide: If you want to go back, I can send you back. No one is making you do this. But know that you came to us for a reason. I have no information on what your situation is out there, but from what I’ve seen, I know that you wouldn’t be here if you had any other choice.
Somni: ...
Guide: Are you ready to begin?
-> Somni: I guess....
    <<set $choice to "A">>
-> Somni: Not yet.
    <<set $choice to "B">>
<<if $choice == "A">>
    Guide: That’s the spirit.
    //Guide snaps her fingers and the room is filled with white light.
    Somni: What happened?
    Guide: Go through the front door.
    Somni: You mean the one with nothing on the other side?
    Guide: Precisely.
    Somni: Okay. But if I die I’m suing.
<<elseif $choice == "B">>
    Somni: Not yet. Just need a little more time to get my bearings.
    Guide: All right. Just let me know when you’re ready!
<<endif>>

===

title: Enter_House
tags:
position: 12, -102
---
Guide: Woah.
Guide: I knew your mind was unstable but I didn't think it'd be THIS bad.
Somni: ...Why does my house look different?
Guide: Don't ask me. It's literally <i>your</i> mind.
Somni: *sigh*
Somni: ...What's the toilet for?
Guide: That I do know actually. It's to <color=green>SAVE</color> your progress.
Guide: There also seems to be some hostiles in your house now. So be careful.
===

title: Guide_Interact
tags:
position: 12, -102
---
Somni: So you're just gonna stand there?
Guide: Part of the procedure is that it's done by you.
Guide: It used to be the other way around but we ran into some legal trouble.
Somni: ...Should I even ask?
Guide: No.
===